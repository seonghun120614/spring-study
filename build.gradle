plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations.all {
	exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
	// Spring Boot Actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// Spring Boot Auto Configure
	implementation 'org.springframework.boot:spring-boot-autoconfigure'

	// Spring Boot devtools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// JPA 아닌 MongoDB 사용 시 querydsl-mongodb 사용
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta' // JPA 기반 QueryDSL 사용
	annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta' // Q 클래스 생성
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'

	// 기본 Spring Boot 스타터에서 로깅은 Logback 이라 Log4j2을 위한 제외 필요
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.spring.framework.boot', module: 'spring-boot-starter-tomcat'
	} // SERVLET, REACTIVE, NONE
	// 더 가볍고 비동기 I/O 를 할 수 있는 jetty 로 실행
	implementation 'org.springframework.boot:spring-boot-starter-jetty'

	// 기본 Spring Boot 스타터에서 로깅은 Logback 이라 제외 필요
	implementation 'org.springframework.boot:spring-boot-starter'

	// log4j2
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	// Bean Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Lombok - getter, setter, toString, Constructor 자동 생성
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	// passay - password 규칙
	implementation 'org.passay:passay:1.6.3'

	// JPA 추가
	implementation('org.springframework.boot:spring-boot-starter-data-jpa') {
		exclude group: 'com.zaxxer', module: 'HikariCP'
	}
	implementation 'org.apache.tomcat:tomcat-jdbc:10.1.20' // 커넥션 풀 tomcat jdbc 사용

	// RDBMS Source 의존성, MySQL
	runtimeOnly 'mysql:mysql-connector-java:8.0.33'

	// RDBMS Source 의존성, PostgreSQL
	runtimeOnly 'org.postgresql:postgresql'

	// RDBMS Source 의존성
	// runtimeOnly 'com.h2database:h2' // 스코프를 runtime 으로만 지정하여 컴파일에서는 사용 안하고, 런타임에서만 사용

	// NoSQL Source 의존성
//	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	// 내장 mongodb
//	implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.11.0'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'junit', module: 'junit' // JUnit 4 제외
		exclude group: 'org.mockito', module: 'mockito-core' // 필요 시
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine' // JUnit 3/4 호환 제거
	}

	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

	// 내장 mongodb 사용
//	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.18.0'

	// log4j-to-slf4j는 제거해야 함 (충돌 발생하므로)
	// log4j-slf4j2-impl 만 유지하면 SLF4J API도 Log4j2에서 처리 가능
	// SLF4J API -> log4j2로 연결
	runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl'
}

// Q 클래스들이 생성될 디렉토리 지정
def querydslDir = "$buildDir/generated/querydsl"

/*
 main 소스 세트에 querydslDir 을 추가함
 build/generated/querydsl 경로에서 생성된 Q-타입 클래스들을
 정상적인 자바 소스처럼 인식, 컴파일 대상으로 포함
 이를 안하면 Q 클래스가 클래스패스에 포함되지 않아 컴파일 오류가 날 수 있음
 */
sourceSets {
	main {
		java {
			srcDir querydslDir
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
}
/*
 Java 컴파일 시 사용되는 annotationProcessor 가 생성한
 파일의 저장 위치를 querydslDir 로 명시
 */
tasks.withType(JavaCompile) {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('processResources') {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

//tasks.named('bootJar') {
//	archiveFileName = 'myapp.jar'
//}